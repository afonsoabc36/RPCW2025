12. Quais são os conceitos ensinados por um determinado mestre (arbitra um)?

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT DISTINCT ?conceito WHERE {
  ?mestre a :Mestre ;
          :nome "Marie Curie" ;
          :ensina ?disciplina .
  ?conceito a :Conceito ;
            :éEstudadoEm ?disciplina .
}

13. Que obras explicam o conceito de "Relatividade"?

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT DISTINCT ?obra WHERE {
  ?conceito a :Conceito ;
            :nome "Relatividade" .
  ?obra a :Obra ;
        :explica ?conceito .
}

14. Em que disciplinas se estudam "Ciências Sociais"?

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT DISTINCT ?disciplina WHERE {
    ?disciplina :pertenceA ?tipoConhec.
  ?tipoConhec a :TipoDeConhecimento ;
            :nome "Ciências Sociais".
}

15. Qual é a aplicação prática dos conhecimentos em "Lógica Formal"?

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT DISTINCT ?aplicacao WHERE {
  ?conceito a :Conceito ;
            :nome "Lógica Formal" ;
            :temAplicaçãoEm ?aplicacao .
}

16. Quais disciplinas pertencem ao tipo de conhecimento "Ciências Exatas"?

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT DISTINCT ?disciplina WHERE {
  ?tipo a :TipoDeConhecimento ;
         :nome "Ciências Exatas" .
  ?disciplina a :Disciplina ;
              :pertenceA ?tipo .
}

17. Que mestres ensinaram no século XIX?

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT DISTINCT ?mestre WHERE {
  ?periodo a :PeríodoHistorico ;
            :nome "Século XIX" .
  ?mestre a :Mestre ;
          :viveuEm ?periodo .
}

18. Que aprendizes estudam "Programação"?

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT DISTINCT ?aprendiz WHERE {
   ?conceito a :Conceito;
    	:nome "Programação";
    	:éEstudadoEm ?disciplina.
  ?aprendiz a :Aprendiz ;
            :aprende ?disciplina .
}

19. (1 val) Qual a distribuição de aprendizes por disciplina? (Resultado será uma lista de pares: Disciplina, número de alunos)

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT ?disciplina (COUNT(?aprendiz) AS ?numeroAprendizes) WHERE {
  ?aprendiz a :Aprendiz ;
            :aprende ?disciplina .
}
GROUP BY ?disciplina

20. (1 val) Qual a distribuição de conceitos por período histórico? (Resultado será uma lista de pares: Período, número de conceitos)

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT ?periodo (COUNT(?conceito) AS ?numeroConceitos) WHERE {
  ?conceito a :Conceito ;
            :surgeEm ?periodo .
}
GROUP BY ?periodo

21. (1.5 val) Quais os títulos das obras associadas a cada conceito? (Resultado será uma lista de pares: Conceito, Lista de títulos de obras)

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT ?conceitoNome (GROUP_CONCAT(?titulo; SEPARATOR=", ") AS ?titulos) WHERE {
  ?conceito a :Conceito ;
            :nome ?conceitoNome .
  ?obra a :Obra ;
        :explica ?conceito ;
        :titulo ?titulo .
}
GROUP BY ?conceitoNome

22. (1.5 val) Quais os aprendizes que frequentam cada uma das disciplinas? (Resultado será uma lista de pares: Disciplina, Lista dos nomes dos aprendizes que frequentam a disciplina)

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT ?disciplinaNome (GROUP_CONCAT(?nomeAprendiz; SEPARATOR=", ") AS ?aprendizes) WHERE {
  ?disciplina a :Disciplina ;
              :nome ?disciplinaNome .
  ?aprendiz a :Aprendiz ;
            :aprende ?disciplina ;
            :nome ?nomeAprendiz .
}
GROUP BY ?disciplinaNome

23. (2 val) Qual a distribuição de aprendizes por escalões etários? (Resultado será uma lista de pares: Escalão etário, número aprendizes nesse escalão); Considera os seguintes escalões: [17, 34], [35,44], [45, 54], [55, 64], [65, 74], [75, 100].

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

SELECT (STR(?escalao) AS ?EscalaoEtario) (COUNT(?aprendiz) AS ?numeroAprendizes) WHERE {
  ?aprendiz a :Aprendiz ;
            :idade ?idade .

  BIND (
    IF(?idade >= 17 && ?idade <= 34, "[17-34]",
    IF(?idade >= 35 && ?idade <= 44, "[35-44]",
    IF(?idade >= 45 && ?idade <= 54, "[45-54]",
    IF(?idade >= 55 && ?idade <= 64, "[55-64]",
    IF(?idade >= 65 && ?idade <= 74, "[65-74]",
    IF(?idade >= 75 && ?idade <= 100, "[75-100]", "Fora do intervalo")))))) AS ?escalao)
}
GROUP BY ?escalao

24. (0.5 val) Cria uma nova relação na ontologia, estudaCom, que relaciona um aluno com um mestre;

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

INSERT DATA {
  :estudaCom rdf:type owl:ObjectProperty ;
             rdfs:domain :Aprendiz ;
             rdfs:range :Mestre .
}

25. (1.5 val) Cria uma query CONSTRUCT para calcular os novos triplos da nova relação estudaCom;
Altera a query para uma query INSERT e acrescenta esses triplos à ontologia;

-- CONSTRUCT

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

CONSTRUCT {
  ?aprendiz :estudaCom ?mestre .
}
WHERE {
  ?aprendiz a :Aprendiz ;
            :aprende ?disciplina .
  ?mestre a :Mestre ;
          :ensina ?disciplina .
}

-- INSERT

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

INSERT {
  ?aprendiz :estudaCom ?mestre .
}
WHERE {
  ?aprendiz a :Aprendiz ;
            :aprende ?disciplina .
  ?mestre a :Mestre ;
          :ensina ?disciplina .
}

26. (0.5 val) Cria uma nova relação na ontologia, dáBasesPara, que relaciona uma disciplina com uma
aplicação;

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

INSERT DATA {
  :dáBasesPara rdf:type owl:ObjectProperty ;
               rdfs:domain :Disciplina ;
               rdfs:range :Aplicação .
}

27. (1.5 val) Cria uma query CONSTRUCT para calcular os novos triplos da nova relação dáBasesPara;
Altera a query para uma query INSERT e acrescenta esses triplos à ontologia.

-- CONSTRUCT

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

CONSTRUCT {
  ?disciplina :dáBasesPara ?aplicacao .
}
WHERE {
  ?conceito :éEstudadoEm ?disciplina ;
            :temAplicaçãoEm ?aplicacao .
}

-- INSERT

PREFIX : <http://www.semanticweb.org/afonsoabc36/ontologies/2025/rpcw2025normal#>

INSERT {
  ?disciplina :dáBasesPara ?aplicacao .
}
WHERE {
  ?conceito :éEstudadoEm ?disciplina ;
            :temAplicaçãoEm ?aplicacao .
}
